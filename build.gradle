plugins {
    alias libs.plugins.com.android.application apply false
    alias libs.plugins.com.android.library apply false
    alias libs.plugins.org.jetbrains.kotlin.android apply false
    alias libs.plugins.com.github.ben.manes.versions
    alias libs.plugins.nl.littlerobots.version.catalog.update
    alias libs.plugins.com.github.triplet.play apply false
}

// https://github.com/ben-manes/gradle-versions-plugin
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    resolutionStrategy {
        componentSelection {
            configureEach {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}

tasks.register('sayHello') {
    doLast {
        println "Hello from Gradle!"
    }
}

tasks.register('sayGoodbye') {
    dependsOn sayHello
    doLast {
        println "Goodbye from Gradle!"
    }
}

tasks.register('deleteZip', Delete) {
    delete "build/project.zip"
}

tasks.register('zipProject', Zip) {
    dependsOn deleteZip
    from '.'
    destinationDirectory = file("build")
    archiveFileName = "project.zip"
    exclude "build/**"
}