//file:noinspection DependencyNotationArgument
plugins {
    alias libs.plugins.com.android.application
    alias libs.plugins.org.jetbrains.kotlin.android
}


android {
    namespace 'com.devexperto.gradletraining'
    compileSdk 34

    defaultConfig {
        applicationId "com.devexperto.gradletraining"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        beta {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ['version', 'abi', 'server']
    productFlavors {
        free {
            dimension 'version'
        }
        premium {
            dimension 'version'
            applicationIdSuffix '.premium'
        }

        arm {
            dimension 'abi'
        }
        arm64 {
            dimension 'abi'
        }
        x86 {
            dimension 'abi'
        }

        dev {
            dimension 'server'
            applicationIdSuffix '.dev'
        }
        staging {
            dimension 'server'
            applicationIdSuffix '.staging'

        }
        prod {
            dimension 'server'
            applicationIdSuffix '.prod'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Bug https://youtrack.jetbrains.com/issue/IDEA-316687/Groovy-warn-incorrectly-about-version-catalog-dependency
    implementation libs.androidx.core.core.ktx
    implementation libs.androidx.lifecycle.lifecycle.runtime.ktx
    implementation libs.bundles.androidx.compose
    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.espresso.core
    androidTestImplementation libs.androidx.compose.ui.ui.test.junit4
    debugImplementation libs.androidx.compose.ui.ui.tooling
    debugImplementation libs.androidx.compose.ui.ui.test.manifest
}
